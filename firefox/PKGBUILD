pkgname="joshuarli-firefox"
pkgver=67.0.2
pkgrel=1
pkgdesc='the firefox web browser'
arch=('x86_64')
license=(MPL GPL LGPL)
url='https://www.mozilla.org/firefox/'
conflicts=('firefox')
provides=('firefox')

# strip not needed; firefox build will do this
options=(!strip)

# depends omitted: libnotify
depends=(libpulse gtk3 libxt)
# mime-types ffmpeg

# makedepends omitted:
# diffutils python2-setuptools mesa imake inetutils python2-psutil
# xorg-server-xvfb required for PGO, which i'm omitting for now due to long build times and some other problems

# this PKGBUILD assumes non base-devel arch linux (e.g. the docker image)
# the required packages from base-devel are as follows: which (required for mach), make, grep, pkgconf, gawk, m4
# autoconf2.13 is specifically required rather than regular autoconf

# python2 required for mach itself, python3.5+ required for some other build stuff
# nasm required for firefox libdavid (av1)

# these are the minimal makedepends necessary to pass ./mach configure for the mozconfig later in the pkgbuild, in order
makedepends=(which make grep python2 python autoconf2.13 pkgconf python
    clang llvm rust cbindgen python nodejs gtk2 nasm unzip zip yasm gawk m4 diffutils)

source=("https://archive.mozilla.org/pub/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.xz")
sha256sums=('6a0dd9a838c9532b143bb0322f73d03afd272f6afba92af5be5845061f0deb47')

# todo
# https://bugzilla.mozilla.org/show_bug.cgi?id=1521249
# --enable-rust-simd

# todo. requires a patch, see LFS notes
# ac_add_options --with-system-graphite2
# ac_add_options --with-system-harfbuzz

# todo
# ac_add_options --enable-optimize=

# todo system libs are preferred rather than building vendored libs, see what works and what doesn't
# ac_add_options --with-system-webp
# ac_add_options --with-system-icu
# ac_add_options --enable-system-ffi
# ac_add_options --enable-system-pixman
# ac_add_options --with-system-bz2
# ac_add_options --with-system-jpeg
# ac_add_options --with-system-png
# ac_add_options --with-system-zlib
# ac_add_options --with-system-libevent  # depends package libevent
# ac_add_options --with-system-nspr  # depends package nspr
# ac_add_options --with-system-nss   # depends package nss
# ac_add_options --enable-system-sqlite   # depends package sqlite

# --disable-printing
# https://bugzilla.mozilla.org/show_bug.cgi?id=1433550

# note: no geolocation/location services
#   * omit wireless-tools, dbus-glib
#   * --disable-dbus
#   * --disable-necko-wifi

prepare () {
    cd "firefox-${pkgver}"
    cat > mozconfig <<EOF
ac_add_options --prefix=/usr
ac_add_options --enable-application=browser

ac_add_options --disable-xcode-checks
ac_add_options --disable-tests
ac_add_options --disable-debug
ac_add_options --disable-debug-symbols
ac_add_options --disable-gtest-in-build
ac_add_options --enable-release
ac_add_options --enable-hardening
ac_add_options --enable-address-sanitizer
ac_add_options --enable-undefined-sanitizer
ac_add_options --enable-optimize
ac_add_options --enable-lto=full
export CC=clang
export CXX=clang++
export AR=llvm-ar
export NM=llvm-nm
export RANLIB=llvm-ranlib

ac_add_options --disable-dbus
ac_add_options --disable-gconf
ac_add_options --disable-necko-wifi

ac_add_options --disable-ctypes
ac_add_options --disable-dbm

ac_add_options --disable-wmf

ac_add_options --disable-synth-speechd
ac_add_options --disable-webspeech
ac_add_options --disable-accessibility

ac_add_options --disable-crashreporter
ac_add_options --disable-updater
EOF
    ./mach configure
}

build () {
    cd "firefox-${pkgver}"
    # SHELL=/usr/sbin/bash
    ./mach build
}

package () {
    cd "firefox-${pkgver}"
    DESTDIR="$pkgdir" ./mach install
    install -Dm755 /dev/stdin "${pkgdir}/usr/bin/firefox" <<END
#!/bin/sh
exec /usr/lib/firefox/firefox "\$@"
END
}
