# slimmed down mpv package compared to the official one

_pkgname=mpv
pkgname="joshuarli-${_pkgname}"
pkgver=0.29.1
pkgrel=1
pkgdesc='a free, open source, and cross-platform media player'
arch=('x86_64')
license=('GPL3')
url='https://mpv.io/'
conflicts=("$_pkgname")
provides=("$_pkgname")
depends=('ffmpeg' 'libxkbcommon' 'libxrandr' 'libxss' 'lua52' 'uchardet' 'libxinerama')
makedepends=('waf' 'mesa' 'vulkan-headers')

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/mpv-player/${_pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('f9f9d461d1990f9728660b4ccb0e8cb5dce29ccaa6af567bec481b79291ca623')

build () {
    cd "${_pkgname}-${pkgver}"
    waf configure --prefix=/usr \
        --confdir=/etc/mpv \
        --lua=52arch \
        --enable-zsh-comp \
        --enable-libmpv-shared \
        --disable-libarchive \
        --disable-libbluray \
        --disable-rubberband \
        --disable-vapoursynth \
        --disable-vapoursynth-lazy \
        --disable-oss-audio \
        --disable-rsound \
        --disable-jack \
        --disable-opensles \
        --disable-xv \
        --disable-caca \
        --disable-tv-v4l2 \
        --disable-libv4l2 \
        --disable-audio-input
    waf build
}

package () {
    cd "${_pkgname}-${pkgver}"
    waf install --destdir="$pkgdir"
}
