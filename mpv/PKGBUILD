# slimmed down mpv package compared to the official one
# assumes X, pulseaudio, and zsh completions are desired

_pkgname=mpv
pkgname="joshuarli-${_pkgname}"
pkgver=0.29.1
pkgrel=1
pkgdesc='a free, open source, and cross-platform media player'
arch=('x86_64')
license=('GPL3')
url='https://mpv.io/'
conflicts=("$_pkgname")
provides=("$_pkgname")
# note: ffmpeg has a lot of the deps we need e.g. libass and various avcodecs
depends=('ffmpeg' 'lua52' 'libxkbcommon' 'libxrandr' 'libxinerama')
makedepends=('waf' 'mesa' 'vulkan-headers')

# lua is required for the useful on screen controller ui buttons and more
# lua 5.3 isn't supported yet: https://github.com/mpv-player/mpv/issues/5205

# i don't need xinerama, but sigh, too lazy to patch upstream
# https://bugs.gentoo.org/617516
# https://github.com/mpv-player/mpv/issues/4404

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/mpv-player/${_pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('f9f9d461d1990f9728660b4ccb0e8cb5dce29ccaa6af567bec481b79291ca623')

# https://raw.githubusercontent.com/mpv-player/mpv/master/wscript
# ignore anything default disabled
# for those that check_pkg_config, explicitly disable unneeded
# for finer grained understanding, actually take some time to parse through waf syntax
# oh, it is so hard to ensure you only get exactly what you need, what a manual process

build () {
    cd "${_pkgname}-${pkgver}"
    waf configure --prefix=/usr \
        --confdir=/etc/mpv \
        --enable-zsh-comp \
        --lua=52arch \
        --disable-javascript \
        --disable-libbluray \
        --disable-uchardet \
        --disable-rubberband \
        --disable-lcms2 \
        --disable-vapoursynth \
        --disable-vapoursynth-lazy \
        --disable-libarchive \
        --disable-oss-audio \
        --disable-rsound \
        --enable-pulse \
        --disable-jack \
        --disable-opensles \
        --disable-alsa \
        --disable-drm \
        --disable-gbm \
        --disable-wayland-protocols \
        --enable-x11 \
        --disable-xv \
        --disable-caca
    waf build
}

package () {
    cd "${_pkgname}-${pkgver}"
    waf install --destdir="$pkgdir"
}
